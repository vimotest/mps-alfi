ext.mpsVersion = "2025.3"
ext.languageName = "alfi"

ext.alfVersion = "1.1.0k"

ext.mpsPlugins = [
]

ext.customMpsPlugins = [
    "TextGenGen-Fum": "https://github.com/Fumapps/textGenGen/releases/download/2025.3.0/TextGenGen.zip",
    "CppBaseLanguage": "https://github.com/Fumapps/CppBaseLanguage/releases/download/2025.3.0/CppBaseLanguage.zip",
    "CsBaseLanguage": "https://github.com/Fumapps/CsBaseLanguage/releases/download/2025.3.0/CsBaseLanguage.zip"
]

apply from: 'gradle/init-gradle-scripts.gradle'

apply from: 'gradle/install-alf.gradle'
apply from: 'gradle/compitest.gradle'
apply from: 'gradle/compitest/generateCsProjFiles.gradle'
apply from: 'gradle/compile-generator-utils.gradle'

task prepareMps {
    group 'mps'
    dependsOn 'buildOsSpecificBundle'
}

// Zipping ouside the build module to avoid path confusion of GeneratorUtils extra jar
task zipAlfiPluginLayout(type: Zip) {
    group 'mps'
    from "$buildDir/artifacts/alfi-build"
    include 'alfi/**'
    archiveFileName = 'alfi.zip'
    destinationDirectory = file("$buildDir/artifacts/alfi-build")
}

task mpsBuild {
    group 'mps'
    dependsOn 'mpsAntGenerate', 'mpsAntBuild', 'zipAlfiPluginLayout'
}
mpsAntBuild.dependsOn mpsAntGenerate
zipAlfiPluginLayout.mustRunAfter mpsAntBuild

task mpsClean {
    group 'mps'
    dependsOn 'mpsAntClean', 'mpsAntCleanSources'
}

task mpsTest {
    group 'mps'
    dependsOn 'mpsAntCheck', 'parseTestReportAfterAntCheck'
}

task openProjectInMps {
    group 'mps'
    dependsOn 'mpsOpenProjectInMpsForCurrentOs'
}

task buildAndOpenProjectInMps {
    group 'mps'
    dependsOn mpsBuild, openProjectInMps
}

task compiTest {
    group 'mps'
    description 'Run the ALF-reference runtime comparison tests'
    dependsOn 'mpsBuild', 'compitestRunTests'
}
compitestRunTests.configure {
    mustRunAfter mpsBuild
}
mpsAntGenerate.dependsOn buildGeneratorUtils
openProjectInMps.dependsOn buildGeneratorUtils
