/*
  Gradle tasks to run the compitest scripts
 */
task compitestRunTests {
    group 'compitest'
    description 'Run the ALF-reference runtime comparison tests'
    dependsOn 'compitestCopyCppBinaries'
    doLast {
        def workingDir = file("$projectDir/scripts/alfCompareTest")
        def scriptFile = file("$workingDir/CompareTestMain.groovy")

        def binding = new Binding([workingDir: workingDir])
        def shell = new GroovyShell(binding)
        shell.getClassLoader().addURL(workingDir.toURI().toURL())
        shell.evaluate(scriptFile)
    }
}

task compitestClean(type: Delete) {
  group 'compitest'
  delete file("build/compitest")
}

task compitestCopyCppBinaries(type: Copy) {
    group 'compitest'
    description 'Copies the built C++ binaries for comparison tests'

    def sourceDir = file("$projectDir/solutions/alfi.compitest/build")
    def targetDir = file("$buildDir/artifacts/alfi-build/tests/compitest-cpp-binaries")

    from sourceDir
    include("*alfi.compitest.Activity*")
    into targetDir
}

task compitestCopyCsBinaries(type: Copy) {
    group 'compitest'
    description 'Copies the built C# binaries for comparison tests'

    def sourceDir = file("$projectDir/solutions/alfi.compitest/dotnet-build/bin")
    def targetDir = file("$buildDir/artifacts/alfi-build/tests/compitest-cs-binaries")

    from sourceDir
    include("**/*.dll")
    include("**/*.runtimeconfig.json")
    into targetDir
    eachFile { fileCopyDetails ->
        fileCopyDetails.path = fileCopyDetails.name
    }
    includeEmptyDirs = false
}
